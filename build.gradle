import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id "java"
    id "org.springframework.boot" version "4.0.1"
    id "io.spring.dependency-management" version "1.1.7"
    id "com.github.spotbugs" version "6.4.2"
    id "checkstyle"
    id "pmd"
    id "jacoco"
}

group = "com.kush.todo"
version = "0.0.1-SNAPSHOT"
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

ext {
    mapStructVersion = "1.6.3"
    nettyDnsResolverVersion = "4.1.110.Final:osx-aarch_64"
    springDocVersion = "3.0.0"
    logstashVersion = "8.1"
    archUnitVersion = "1.4.1"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.boot:spring-boot-starter-kafka"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-liquibase"
    implementation "org.springframework.boot:spring-boot-starter-webmvc"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-aspectj"
    implementation "org.springframework.boot:spring-boot-starter-security-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-micrometer-tracing-brave"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "io.micrometer:micrometer-tracing-bridge-brave"
    implementation "org.postgresql:postgresql"
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"

    runtimeOnly "io.netty:netty-resolver-dns-native-macos:${nettyDnsResolverVersion}" //to remove warning for MacOS

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

    testImplementation "org.springframework.boot:spring-boot-micrometer-tracing-test"
    testImplementation "org.springframework.boot:spring-boot-starter-actuator-test"
    testImplementation "org.springframework.boot:spring-boot-starter-data-jdbc-test"
    testImplementation "org.springframework.boot:spring-boot-starter-kafka-test"
    testImplementation "org.springframework.boot:spring-boot-starter-liquibase-test"
    testImplementation "org.springframework.boot:spring-boot-starter-security-oauth2-resource-server-test"
    testImplementation "org.springframework.boot:spring-boot-starter-security-test"
    testImplementation "org.springframework.boot:spring-boot-starter-validation-test"
    testImplementation "org.springframework.boot:spring-boot-starter-webmvc-test"
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.testcontainers:testcontainers-junit-jupiter"
    testImplementation "org.testcontainers:testcontainers-kafka"
    testImplementation "com.redis:testcontainers-redis"
    testImplementation "org.testcontainers:testcontainers-postgresql"
    testImplementation "com.tngtech.archunit:archunit-junit5:${archUnitVersion}"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.named("test") {
    jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
    useJUnitPlatform()
}

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

tasks.register("integrationTest", Test) {
    jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
    useJUnitPlatform()

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

rootProject.tasks.named("processIntegrationResources") {
    duplicatesStrategy = "include"
}

if (System.properties["test.profile"] == "integration") {
    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    tasks.named("jacocoTestReport") {
        dependsOn integrationTest
    }
}

tasks.named("jacocoTestReport") {
    dependsOn test
    executionData fileTree(project.rootDir.absolutePath).include(
            "**/build/jacoco/test.exec",
            "**/build/jacoco/integrationTest.exec"
    )

    classDirectories.setFrom(
            fileTree(dir: layout.buildDirectory.dir("classes/java/main").get().asFile, excludes: [
                    "com/kush/todo/dto/**",
                    "com/kush/todo/entity/**",
                    "com/kush/todo/mapper/**",
                    "com/kush/todo/constant/**",
                    "com/kush/todo/exception/**",
                    "com/kush/todo/config/**",
                    "com/kush/todo/ToDoApplication.class"
            ])
    )

    reports {
        xml.required = true
        html.required = true
    }
}

checkstyle {
    toolVersion = "11.0.1"
    configFile = file("config/checkstyle-rules.xml")
    ignoreFailures = false
}

pmd {
    toolVersion = "7.17.0"
    ruleSets = []
    ruleSetFiles = files("config/pmd-rules.xml")
    ignoreFailures = false
}

spotbugs {
    toolVersion = "4.9.4"
    effort = Effort.MAX
    reportLevel = Confidence.valueOf("LOW") //A workaround to overcome build warning to to plugin issues
    ignoreFailures = false
    excludeFilter = file("config/spotbugs-exclusions.xml")
}

tasks.withType(Checkstyle).configureEach {
    reports {
        html.required = true
        xml.required = true
    }
}

tasks.withType(Pmd).configureEach {
    reports {
        html.required = true
        xml.required = true
    }
}

tasks.withType(SpotBugsTask).configureEach {
    reports {
        html.required = true
        xml.required = true
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            "-Amapstruct.defaultComponentModel=spring",
            "-Werror",
            "-Amapstruct.unmappedTargetPolicy=ERROR"
    ]
}

tasks.named("check") {
    dependsOn tasks.named("spotbugsMain")
    dependsOn tasks.named("spotbugsTest")
    dependsOn tasks.named("checkstyleMain")
    dependsOn tasks.named("checkstyleTest")
    dependsOn tasks.named("pmdMain")
    dependsOn tasks.named("pmdTest")
}